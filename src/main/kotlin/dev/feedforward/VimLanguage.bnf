{
  parserClass="dev.feedforward.parser.VimParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Vim"
  psiImplClassSuffix="Impl"
  psiPackage="dev.feedforward.psi"
  psiImplPackage="dev.feedforward.psi.impl"

  elementTypeHolderClass="dev.feedforward.psi.VimTypes"
  elementTypeClass="dev.feedforward.psi.VimElementType"
  tokenTypeClass="dev.feedforward.psi.VimTokenType"

  tokens = [
      ANDAND             = '&&'
      OROR               = '||'
      EQEQ               = '=='
      EQEQCI             = '==?'
      EQEQCS             = '==#'
      NEQ                = '!='
      NEQCI              = '!=?'
      NEQCS              = '!=#'
      GT                 = '>'
      GTCI               = '>?'
      GTCS               = '>#'
      GTEQ               = '>='
      GTEQCI             = '>=?'
      GTEQCS             = '>=#'
      LT                 = '<'
      LTCI               = '<?'
      LTCS               = '<#'
      LTEQ               = '<='
      LTEQCI             = '<=?'
      LTEQCS             = '<=#'
      MATCH              = '=~'
      MATCHCI            = '=~?'
      MATCHCS            = '=~#'
      NOMATCH            = '!~'
      NOMATCHCI          = '!~?'
      NOMATCHCS          = '!~#'
      IS                 = 'is'
      ISCI               = 'is?'
      ISCS               = 'is#'
      ISNOT              = 'isnot'
      ISNOTCI            = 'isnot?'
      ISNOTCS            = 'isnot#'
      PLUS               = '+'
      MINUS              = '-'
      DOT                = '.'
      STAR               = '*'
      SLASH              = '/'
      PERCENT            = '%'
      NOT                = '!'
      ASS                = '='

      SQOPEN             = '['
      SQCLOSE            = ']'
      COLON              = ':'
      POPEN              = '('
      PCLOSE             = ')'

      ECHO = "echo"
      OPEN_QUOTE = '"'
      CLOSE_QUOTE = '"'
      STRING_LITERAL = 'regexp:("([^"\\]|\\.)*")'

      HEX_NUMBER = 'regexp:0[xX][0-9a-fA-F]+'
      INT_NUMBER = 'regexp:\d+'
      FLOAT_NUMBER = 'regexp:\d+\.\d+'
      SCIENTIFIC_NUMBER = 'regexp:\d+\.\d+([eE])(([+-])?\d+)'

      IDENTIFIER = 'regexp:[0-9A-Za-z_]'

      space='regexp:\s+'
  ]

  extends(".*expr")=expr
}

vimFile ::= item_*

private item_ ::= (echo_command|COMMENT|CRLF)

echo_command ::= ECHO expr*


expr ::= logical_or_expr
       | logical_and_expr
       | comparison_expr
       | add_expr
       | mul_expr
       | unary_expr
       | squares_group_expr
       | final_expr

private squares_group_expr ::= squares_expr | call_expr
private final_expr ::= literal_expr | parentheses_expr

logical_or_expr ::= expr '||' expr
logical_and_expr ::= expr '&&' expr
comparison_expr ::= expr ('==' | '==?' | '==#' | '!=' | '!=?' | '!=#' | '>' | '>?' | '>#' | '>=' | '>=?' | '>=#' | '<' | '<?' | '<#' | '<=' | '<=?' | '<=#' | '=~' | '=~?' | '=~#' | '!~' | '!~?' | '!~#' | 'is' | 'is?' | 'is#' | 'isnot' | 'isnot?' | 'isnot#') expr
add_expr ::= expr ('+' | '-' | '.') expr
mul_expr ::= expr ('*' | '/' | '%') expr
unary_expr ::= ('!' | '+' | '-') expr
literal_expr ::= number | STRING_LITERAL
parentheses_expr ::= '(' expr ')'

squares_expr ::= expr (bracket_slice|subscript)
bracket_slice ::= SQOPEN expr? ':' expr? SQCLOSE
subscript ::= SQOPEN expr SQCLOSE
call_expr ::= expr arg_list
// Vim allows foo(a, b, ).
arg_list ::= '(' (')' | expr (',' expr)* ','? ')')

number ::= HEX_NUMBER | INT_NUMBER | FLOAT_NUMBER | SCIENTIFIC_NUMBER
