{
  parserClass="dev.feedforward.vim.lang.parser.VimParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Vim"
  psiImplClassSuffix="Impl"
  psiPackage="dev.feedforward.vim.lang.psi"
  psiImplPackage="dev.feedforward.vim.lang.psi.impl"

  elementTypeHolderClass="dev.feedforward.vim.lang.psi.VimTypes"
  elementTypeClass="dev.feedforward.vim.lang.psi.VimElementType"
  tokenTypeClass="dev.feedforward.vim.lang.psi.VimTokenType"

  tokens = [
      ANDAND             = '&&'
      OROR               = '||'
      EQEQ               = '=='
      EQEQCI             = '==?'
      EQEQCS             = '==#'
      NEQ                = '!='
      NEQCI              = '!=?'
      NEQCS              = '!=#'
      GT                 = '>'
      GTCI               = '>?'
      GTCS               = '>#'
      GTEQ               = '>='
      GTEQCI             = '>=?'
      GTEQCS             = '>=#'
      LT                 = '<'
      LTCI               = '<?'
      LTCS               = '<#'
      LTEQ               = '<='
      LTEQCI             = '<=?'
      LTEQCS             = '<=#'
      MATCH              = '=~'
      MATCHCI            = '=~?'
      MATCHCS            = '=~#'
      NOMATCH            = '!~'
      NOMATCHCI          = '!~?'
      NOMATCHCS          = '!~#'
      IS                 = 'is'
      ISCI               = 'is?'
      ISCS               = 'is#'
      ISNOT              = 'isnot'
      ISNOTCI            = 'isnot?'
      ISNOTCS            = 'isnot#'
      PLUS               = '+'
      MINUS              = '-'
      DOT                = '.'
      STAR               = '*'
      SLASH              = '/'
      PERCENT            = '%'
      BANG               = '!'
      ASS                = '='

      SQOPEN             = '['
      SQCLOSE            = ']'
      COLON              = ':'
      POPEN              = '('
      PCLOSE             = ')'
      QUESTION           = '?'

      C_ECHO = "regexp:echo|ech|ec"
      C_ASCII = "regexp:ascii|asci|asc|as"
      C_COMCLEAR = "regexp:comclear|comclea|comcle|comcl|comc"
      C_ONLY = "regexp:only|onl|on"
      C_QUIT = "regexp:quit|qui|qu|q"

      OPEN_QUOTE = '"'
      CLOSE_QUOTE = '"'
      STRING_LITERAL = 'regexp:("([^"\\]|\\.)*")'

      HEX_NUMBER = 'regexp:0[xX][0-9a-fA-F]+'
      INT_NUMBER = 'regexp:\d+'
      FLOAT_NUMBER = 'regexp:\d+\.\d+'
      SCIENTIFIC_NUMBER = 'regexp:\d+\.\d+([eE])(([+-])?\d+)'

      IDENTIFIER = 'regexp:[0-9A-Za-z_]'

      space='regexp:\s+'
  ]

  extends(".*expr")=expr
}

vimFile ::= item_*

private item_ ::= (command|COMMENT|CRLF)

command ::= echo_command
          | ascii_command
          | comclear_command
          | only_command
          | quit_command

echo_command ::= C_ECHO expr*
ascii_command ::= C_ASCII
comclear_command ::= C_COMCLEAR
only_command ::= <<bang C_ONLY>>
quit_command ::= <<bang C_QUIT>>

private meta bang ::= <<p>> [BANG]

expr ::= ternary_expr
       | logical_or_expr
       | logical_and_expr
       | comparison_expr
       | add_expr
       | mul_expr
       | unary_expr
       | squares_group_expr
       | final_expr

private squares_group_expr ::= squares_expr | call_expr
private final_expr ::= literal_expr | parentheses_expr

ternary_expr ::= expr '?' expr ':' expr
logical_or_expr ::= expr '||' expr
logical_and_expr ::= expr '&&' expr
comparison_expr ::= expr ('==' | '==?' | '==#' | '!=' | '!=?' | '!=#' | '>' | '>?' | '>#' | '>=' | '>=?' | '>=#' | '<' | '<?' | '<#' | '<=' | '<=?' | '<=#' | '=~' | '=~?' | '=~#' | '!~' | '!~?' | '!~#' | 'is' | 'is?' | 'is#' | 'isnot' | 'isnot?' | 'isnot#') expr
add_expr ::= expr ('+' | '-' | '.') expr
mul_expr ::= expr ('*' | '/' | '%') expr
unary_expr ::= ('!' | '+' | '-') expr
literal_expr ::= number | STRING_LITERAL
parentheses_expr ::= '(' expr ')'

squares_expr ::= expr (bracket_slice|subscript)
bracket_slice ::= SQOPEN expr? ':' expr? SQCLOSE
subscript ::= SQOPEN expr SQCLOSE
call_expr ::= expr arg_list
// Vim allows foo(a, b, ).
arg_list ::= '(' expr (',' (expr | &')'))* ')' {pin(".*")=1}

number ::= HEX_NUMBER | INT_NUMBER | FLOAT_NUMBER | SCIENTIFIC_NUMBER
